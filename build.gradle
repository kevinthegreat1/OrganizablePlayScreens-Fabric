plugins {
	id 'fabric-loom' version '1.13-SNAPSHOT'
	id 'maven-publish'
	id "me.modmuss50.mod-publish-plugin" version "1.1.0"
}

version = project.mod_version + '+' + project.minecraft_version
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

repositories {
	maven { url = "https://maven.terraformersmc.com/releases/" }
    maven { url = "https://repo.essential.gg/repository/maven-public/" }
}

loom {
	accessWidenerPath = file("src/main/resources/organizableplayscreens.accesswidener")
}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	testImplementation "net.fabricmc:fabric-loader-junit:${project.loader_version}"

	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
    productionRuntimeMods "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	modImplementation "com.terraformersmc:modmenu:${project.modmenu_version}"
    productionRuntimeMods "com.terraformersmc:modmenu:${project.modmenu_version}"
	modCompileOnly "gg.essential:essential-${project.essential_minecraft_version}-fabric:${project.essential_version}"
}

fabricApi {
	configureTests {
		createSourceSet = true
		modId = "${project.name}-test"
		enableGameTests = false
		enableClientGameTests = true
		eula = true
	}
}

tasks.register("runProductionClientGameTest", net.fabricmc.loom.task.prod.ClientProductionRunTask) {
    jvmArgs.addAll([
            "-Dfabric.client.gametest",
            "-Dfabric.client.gametest.disableNetworkSynchronizer=true"
    ])
    useXVFB = true
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 21
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.base.archivesName.get()}"}
	}
}

test {
	useJUnitPlatform()
}

// configure the maven publication
publishing {
	publications {
		create("mavenJava", MavenPublication) {
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
	}
}

publishMods {
	file = remapJar.archiveFile
	displayName = "Folders! ${mod_version} for ${minecraft_version}"
	changelog = rootProject.file("CHANGELOG.md").text
	type = STABLE
	modLoaders.add("fabric")

	curseforge {
		projectId = "687113"
		projectSlug = "organizable-play-screens" // Required for discord webhook
		accessToken = providers.environmentVariable("CURSEFORGE_TOKEN")
		minecraftVersionRange {
			start = "1.21.9"
			end = "1.21.10"
		}
		requires("fabric-api")
	}
	modrinth {
		projectId = "zKrZtg3n"
		accessToken = providers.environmentVariable("MODRINTH_TOKEN")
		minecraftVersionRange {
			start = "1.21.9"
			end = "1.21.10"
		}
	}
	github {
		repository = "kevinthegreat1/OrganizablePlayScreens-Fabric"
		accessToken = providers.environmentVariable("GITHUB_TOKEN")
		commitish = "main"
	}
}
